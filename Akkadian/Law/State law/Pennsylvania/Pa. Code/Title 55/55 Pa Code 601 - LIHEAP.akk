# Namespace:    Pa.Code.Tit55.Ch601
# Summary:      Low Income Home Energy Assistance Program (LIHEAP)
# Updated:      2013-03-18
# Author:       Michael Poulshock
# Source:       http://services.dpw.state.pa.us/oimpolicymanuals/manuals/bop/le/LIHEAP_State_Plan_2012_Appendix_B.pdf


# 601.31 - GENERAL ELIGIBILITY REQUIREMENTS

# >>Does {1} meet the general eligibility requirements for LIHEAP in Pennsylvania?
Tbool IsEligible(Thing household) =
    MeetsIncomeTest(household) &
    ResponsibleForHeatingCosts(household) &
    PaResidents(household) &
    LawfullyPresent(household)
    
# Income test
# TODO: Countable household members only?
Tbool MeetsIncomeTest(Thing hh) =
    LIHEAPIncome(hh) <= USC.Tit42.Sec9902.FPGPercentage(Mems(hh).Count, "Pennsylvania", threshold)

    Tnum threshold =
        set:
        from 2012-12-31 -> Stub()
        from 2011-01-01 -> 1.50     # TODO: Check these dates
        else Stub()

# >>Is {1} responsible for home heating costs?
Tbool ResponsibleForHeatingCosts(Thing hh)
    
# At least one household member is a PA resident (assumption)
Tbool PaResidents(Thing hh) =
    Mems(hh).Exists(StateOfResidence(_) == "Pennsylvania")
    
# At least one household member is lawfully present in the U.S. (assumption)
Tbool LawfullyPresent(Thing hh) =
    Mems(hh).Exists(IsLawfullyPresent(_))
   
# Lawful presence
Tbool IsLawfullyPresent(Thing p) =
    Imm.IsUSCitizen(p) |
    IsQLAN(p)
    
# Qualified lawfully admitted noncitizen
Tbool IsQLAN(Thing p) =
    USC.Tit8.Sec1641.IsQualifiedAlien(p)
    

# HOUSEHOLD SIZE

# 601.41(1) - Household size
Tnum HouseholdSize(Thing hh) =
    CountableMembers(hh).Count

# Countable household members
Tset CountableMembers(Thing hh) =
    Mems(hh).Filter(IsCountable(_))

# Household member should be counted
Tbool IsCountable(Thing p) =
    ! PreviouslyReceivedLIHEAPInAnotherHousehold(p) &
    Econ.LivingArrangement(p) <> "Temporary living arrangement" &
    Econ.LivingArrangement(p) <> "Reside in institution" &
    ! IsQLAN(p) &
    ! IsInPrison(p) &
    ! IsFleeingFelon(p)     # TODO: Left out people fleeing NJ high misdemeanors
     
# Household members
Tset Mems(Thing household) =
    Econ.HouseholdMembers(household)


# HOUSEHOLD (ANNUAL) INCOME FOR ELIGIBILITY PURPOSES    

# 601.41(2) - LIHEAP income
Tnum LIHEAPIncome(Thing hh) =
    set:
    if AnnualEmploymentIncome(hh) > 0 -> TotalCountableIncome(hh) - (AnnualEmploymentIncome(hh) * 0.20)
    else TotalCountableIncome(hh)
    
Tnum AnnualEmploymentIncome(Thing hh) =
    MembersWithCountableIncome(hh).Sum(Econ.AnnualWages(_))

# Total countable income from all countable household members
Tnum TotalCountableIncome(Thing hh) =
    MembersWithCountableIncome(hh).Sum(TreatedIncome(_))
    
Tset MembersWithCountableIncome(Thing hh) =
    Mems(hh).Filter(IncomeIsCounted(_))

# 601.81
Tbool IncomeIsCounted(Thing p) = 
    true        # TODO: Refine...


# TREATMENT OF INCOME

# 601.83(a) - "Treated" (annual) income
# Applicant may choose whether the time period to be used in determining gross annual 
# income shall be for the 12 months or the 90 days before the date of application.
# Regardless of the selected time period, income shall be converted to a yearly figure.
# TODO: Special rules for people receiving certain DPW benefits.
Tnum TreatedIncome(Thing p) =
    Min(Month12Income(p), Day90Income(p) * (DaysInYear()/90)).RoundToNearest(0.01)   # Applicant wants lower of the two

# Income accrued in the prior 12 months
Tnum Month12Income(Thing p) =
    CountableIncome(p).SlidingSummedIntervals(TheMonth, 12).Shift(-1, TheYear)

# Income accrued in the prior 90 days
Tnum Day90Income(Thing p) =
    DailyIncome(p).SlidingSummedIntervals(TheDay, 90).Shift(-90, TheDay)

# Daily income
Tnum DailyIncome(Thing p) =
    CountableIncome(p) / DaysInMonth()
    
    
Test: TreatedIncome1
- Thing p
- Pa.Code.Tit55.Ch601.CountableIncome(p) = $1000
- Pa.Code.Tit55.Ch601.DailyIncome(p).RoundToNearest(0.01).AsOf(2010-01-01).Out =?= $32.26

Test: TreatedIncome2
- Thing p
- Pa.Code.Tit55.Ch601.CountableIncome(p) = $1000
- Pa.Code.Tit55.Ch601.Month12Income(p).RoundToNearest(0.01).AsOf(2015-01-01).Out =?= $12,000.00

Test: TreatedIncome3_Recently_lost_job
- Thing p
- Pa.Code.Tit55.Ch601.CountableIncome(p) = {1800-01-01: $2,000; 2015-05-01: $0}
- Pa.Code.Tit55.Ch601.TreatedIncome(p).AsOf(2015-06-15).Out =?= $12,035.84


# COUNTABLE MONTHLY INCOME

# 601.81 - Total countable income
Tnum CountableIncome(Thing p) =
    GrossIncome(p) - ExcludedIncome(p)

# 601.82
Tnum GrossIncome(Thing p) =
    EarnedIncome(p) + UnearnedIncome(p)

Tnum EarnedIncome(Thing p) =
    Econ.MonthlyWages(p) +
    Ch183.Sec65.MonthlyProfit(p)      # Encompasses both 601.82(2) and (3)

Tnum UnearnedIncome(Thing p) =
    Econ.MonthlyUnearnedIncome(p) 
            
# 601.84
# >>How much of {1}'s monthly income should be excluded under 55 Pa. Code 601.84?
Tnum ExcludedIncome(Thing p)
    
             
# GENERAL LIHEAP BENEFIT AMOUNT

# Depends on: PACountyOfResidence, LIHEAPIncome, Econ.HeatingFuelType, and HouseholdSize.
# If the rules can't be found elsewhere, they have to be reverse engineered from this webpage:
# http://www.dpw.state.pa.us/foradults/heatingassistanceliheap/liheapbenefitamounttable/index.htm


# INPUTS 

# >>Did {1} previously receive a LIHEAP benefit in another household during the program year?
Tbool PreviouslyReceivedLIHEAPInAnotherHousehold(Thing person)


# UNIT TESTS

Test: 403207628
- Thing Joneses
- Things sam
- Econ.HouseholdMembers(Joneses) = [[sam]]
- Econ.MonthlyWages(sam) = 400
- Pa.Code.Tit55.Ch183.Sec65.MonthlyProfit(sam) = 0
- Econ.MonthlySocialSecurityBenefits(sam) = 0
- Econ.MonthlySupplementalSecurityIncome(sam) = 0
- Econ.MonthlyVeteransBenefits(sam) = 0
- Econ.MonthlyChildSupportIncome(sam) = 0
- Econ.MonthlyUnemploymentCompensation(sam) = 0
- Pa.Code.Tit55.Ch601.ExcludedIncome(sam) = 0
- Pa.Code.Tit55.Ch601.ResponsibleForHeatingCosts(Joneses) = true
- StateOfResidence(sam) = "Pennsylvania"
- Imm.USImmigrationStatus(sam) = "U.S. citizen"
- Pa.Code.Tit55.Ch601.IsEligible(Joneses).Out =?= "{Dawn: Stub; 1/20/2011: true; 12/31/2012: Stub}"

