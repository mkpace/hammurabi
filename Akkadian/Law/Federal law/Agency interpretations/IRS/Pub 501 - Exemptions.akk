# Namespace:    Hammurabi.IRS.Pub501
# Summary:      IRS Publication 501 - Exemptions for individuals
# Updated:      2012-10-15
# Author:       Michael Poulshock
# References:   Hammurabi.USC.Tit26


# Total amount that a person can deduct for exemptions
Tnum ExemptionAmount(Thing p) =
    Sec151.ExemptionAmountAfterPhaseout(p)
    
# Exemption amount before the phaseout (which is calculated in USC.Tit26.Sec151)
Tnum ExemptionAmountBeforePhaseout(Thing p) =
    TotalClaimableExemptions(p) * ExemptionAmountPerPerson(p)

# Total exemptions the person can claim on Form 1040, line 6d
Tnum TotalClaimableExemptions(Thing p) =
       set:
    if Peo.IsDeceased(p)                    -> Stub()       # See "Death of spouse", p. 10
    if certainResidentsIndiaKorea       -> Stub()        # See Pub. 519; sec 873(b)(3)
    if IRS.Pub519.IsDualStatusAlien(p)  -> Stub()        #    " "
    if CertainNonresidentAliens(p)      -> OwnExemption(p)
    else NumberOfExemptions(p)
    
    Tbool certainResidentsIndiaKorea =
        Peo.CountryOfResidence(p) == "India" |
        Peo.CountryOfResidence(p) == "Korea" 
        
# Is a NRA, unless from Canada or Mexico
Tbool CertainNonresidentAliens(Thing p) =
    IRS.Pub519.IsNonresidentAlien(p) &
    Peo.CountryOfResidence(p) <> "Canada" &
    Peo.CountryOfResidence(p) <> "Mexico"

# Exemption amount per person (varies by tax year)
# Source: http://taxes.about.com/od/preparingyourtaxes/a/personal_exempt.htm
Tnum ExemptionAmountPerPerson(Thing p) =
    set:
    if year == 2012 -> $3,800
    if year == 2011 -> $3,700
    if year == 2010 -> $3,650
    if year == 2009 -> $3,650
    if year == 2008 -> $3,500
    if year == 2007 -> $3,400
    if year == 2006 -> $3,300
    if year == 2005 -> $3,200
    if year == 2004 -> $3,100
    if year == 2003 -> $3,050
    if year == 2002 -> $3,000
    if year == 2001 -> $2,900
    if year == 2000 -> $2,800
    if year == 1999 -> $2,750
    else Stub()
    
    Tnum year = Sec441.TaxYear(p)
    
# Personal + dependency exemptions
Tnum NumberOfExemptions(Thing p) =
    PersonalExemptions(p) +
    DependencyExemptions(p)
        
# Number of personal exemptions
Tnum PersonalExemptions(Thing p) = 
    OwnExemption(p) + SpouseExemption(p)
    
# Own exemption
Tnum OwnExemption(Thing p) =
    set:
    if Sec152.CanBeClaimedAsDependentBySomeone(p) -> 0
    else 1
    
# TODO: Test using temporal sets for SpouseOf()
# Spousal exemption
Tnum SpouseExemption(Thing p) =
    set:
    if SpouseDiedAndPersonDidNotRemarry(p)  -> 1
    if ! MarriedAtEndOfTaxYear(p)           -> 0
    if SpouseDiedDuringTaxYear(p)           -> 0    # Person remarried
    if MFJAndUnclaimedSpouse(p)             -> 1    # Person and spouse alive
    if MeetsMFSorHoHTest(p)                 -> 1    #   " "
    else 0

# Spouse died and person remarried during tax year
Tbool SpouseDiedAndPersonDidNotRemarry(Thing p) =
    ! MarriedAtEndOfTaxYear(p) &
    SpouseDiedDuringTaxYear(p)
    
# Spouse died during tax year
Tbool SpouseDiedDuringTaxYear(Thing p) =
    MarriedAtEndOfTaxYear(p) &
    Fam.SpousesOf(p).Exists(DiedDuringTaxYear(_))
 
# True during the tax year in which a person died
Tbool DiedDuringTaxYear(Thing p) =
    Peo.IsDeceased(p).IsEverTrue() &
    Peo.DateOfDeath(p).IsInPeriod(Sec441.TaxYear(p))
    
# Married on last day of tax year 
Tbool MarriedAtEndOfTaxYear(Thing p) =
    Fam.IsMarried(p).PeriodEndVal(Sec441.TaxYear(p))
    
# MFJ, no one can claim spouse    
Tbool MFJAndUnclaimedSpouse(Thing p) =
    Sec2.FilingStatus(p) == "Married filing jointly" &
    Fam.SpousesOf(p).Exists(! Sec152.CanBeClaimedAsDependentBySomeone(_))
    
# Test for married filing separately and head of household
Tbool MeetsMFSorHoHTest(Thing p) =
    ...
        Sec2.FilingStatus(p) == "Married filing separately" |
        Sec2.FilingStatus(p) == "Head of household"
    &
    Fam.SpousesOf(p).ForAll(MeetsSpouseTest(_))
    
# Spouse test for MFS and HoH
Tbool MeetsSpouseTest(Thing spouse) =
    GrossIncome(spouse) == 0 &
    Sec2.FilingStatus(spouse) == "Not filing" &
    Sec152.CanBeClaimedAsDependentBySomeone(spouse)
    
# >>How many dependency exemptions does/did {1} claim?  (This is the number of people that {1} will claim as a dependent on their federal tax return.)
Tnum? DependencyExemptions(Thing p) =
    possibleDeps.Filter(Sec152.IsDependentOf(p,_)).Count
    
    Tset possibleDeps = Sec152.PeopleWhoMightClaimAsDependent(p)
    
    
# TODO: Move elsewhere...
# >>What is/was {1}'s annual gross income?
Tnum GrossIncome(Thing p)
    

# UNIT TESTS

Test: USCitizenMFJ
- Thing jed
- Peo.IsDeceased(jed) = false
- Peo.CountryOfResidence(jed) = "United States"
- Imm.USImmigrationStatus(jed) = "U.S. citizen"
- USC.Tit26.Sec152.CanBeClaimedAsDependentBySomeone(jed) = false
- Fam.MaritalStatus(jed) = "Married"
- USC.Tit26.Sec2.FedTaxFilingStatus(jed) = "Married filing jointly"
- Things tammy
- Fam.SpousesOf(jed) = [[tammy]]
- Peo.IsDeceased(tammy) = false
- USC.Tit26.Sec152.CanBeClaimedAsDependentBySomeone(tammy) = false
- IRS.Pub501.DependencyExemptions(jed) = 0
- IRS.Pub501.ExemptionAmountBeforePhaseout(jed).AsOf(2011-12-31).Out =?= $7,400

Test: USCitizenSingle
- Thing jimbo
- Peo.IsDeceased(jimbo) = false
- Peo.CountryOfResidence(jimbo) = "United States"
- Imm.USImmigrationStatus(jimbo) = "U.S. citizen"
- USC.Tit26.Sec152.CanBeClaimedAsDependentBySomeone(jimbo) = false
- Fam.MaritalStatus(jimbo) = "Single"
- USC.Tit26.Sec2.FedTaxFilingStatus(jimbo) = "Single"
- IRS.Pub501.DependencyExemptions(jimbo) = 1
- IRS.Pub501.ExemptionAmountBeforePhaseout(jimbo).AsOf(2011-12-31).Out =?= $7,400

Test: 631329891
- Thing larry
- Peo.IsDeceased(larry) = false
- Peo.CountryOfResidence(larry) = "Russia"
- Imm.USImmigrationStatus(larry) = "Alien"
- IRS.Pub519.AlienTaxStatus(larry) = "Dual-status alien"
- IRS.Pub501.ExemptionAmountBeforePhaseout(larry).Out =?= "Stub"

Test: 243734645
- Thing larry
- Peo.IsDeceased(larry) = false
- Peo.CountryOfResidence(larry) = "Japan"
- Imm.USImmigrationStatus(larry) = "Alien"
- IRS.Pub519.AlienTaxStatus(larry) = "Nonresident alien"
- USC.Tit26.Sec152.CanBeClaimedAsDependentBySomeone(larry) = false
- IRS.Pub501.ExemptionAmountBeforePhaseout(larry).AsOf(2011-12-31).Out =?= $3,700

Test: 385282068
- Thing larry
- Peo.IsDeceased(larry) = false
- Peo.CountryOfResidence(larry) = "India"
- IRS.Pub501.ExemptionAmountBeforePhaseout(larry).Out =?= "Stub"

Test: DiedDuringTaxYear1
- Thing jim
- Peo.IsDeceased(jim) = true
- Peo.DateOfDeath(jim) = 2010-04-04
- IRS.Pub501.DiedDuringTaxYear(jim).Out =?= "{Dawn: false; 1/1/2010: true; 1/1/2011: false}"

Test: DiedDuringTaxYear2
- Thing jim
- Peo.IsDeceased(jim) = false
- IRS.Pub501.DiedDuringTaxYear(jim).Out =?= false

