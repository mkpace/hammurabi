# Citation:     42 U.S.C. 416 (2012)
# Namespace:    Hammurabi.USC.Tit42.Sec416
# Summary:      Social Security Administration retirement age.
# Updated:      2012-05-24
# Author:       Michael Poulshock


# STATUTORY LOGIC

# 416(l)(1) - Reached retirement age
Tbool ReachedRetirementAge(Thing p) =
    Peo.AgeInMonths(p) >= RetirementAgeInMonths(p)

# 416(l)(1) - Retirement age
Tnum RetirementAgeInMonths(Thing p) =
    set:
    if ret < 2000-01-01 -> 65 * 12
    if ret < 2005-01-01 -> (65 * 12) + AgeIncreaseFactor(p)
    if ret < 2017-01-01 -> (66 * 12)
    if ret < 2022-01-01 -> (66 * 12) + AgeIncreaseFactor(p)
    else 67 * 12
    
    Tdate ret =
        EarlyRetirementDate(p)

# 416(l)(3) - Age increase factor
Tnum AgeIncreaseFactor(Thing p) =
    set:
    if retYear >= 2000 & retYear <= 2004 -> 2 * (retYear - 2000 + 1)
    if retYear >= 2017 & retYear <= 2021 -> 2 * (retYear - 2017 + 1)
    else 0
    
    # Note: (2/12) * ((retYear - 2000 + 1) * 12) = 2 * (retYear - 2000 + 1)
    
    Tnum retYear = 
        EarlyRetirementDate(p).Year
        
# Early retirement date
Tdate EarlyRetirementDate(Thing p) =
    ReachedEarlyRetirement(p).DateFirstTrue

# Reached early retirement
Tbool ReachedEarlyRetirement(Thing p) =
    SSAAge(p) >= EarlyRetirementAge(p)
    
# 416(l)(2) - Early retirement age
Tnum EarlyRetirementAge(Thing p) =
    set:
    if Fam.IsWidowed(p) -> 60
    else 62

# SSA age
Tnum SSAAge(Thing p) =
    Time.IntervalsSince(dob, dob.AddYears(110), Time.IntervalType.Year)
    
    Tdate dob =
        SSADateOfBirth(p)
        
# SSA date of birth
# If born on Jan. 1, use previous year
# If born on first of month, use previous month
# See www.ssa.gov/oact/ProgData/nra.html        
Tdate SSADateOfBirth(Thing p) =
    set:
    if Peo.DoB(p).Day == 1 -> Peo.DoB(p).AddDays(-1)
    else Peo.DoB(p)


# CALCULATIONS PER SSA WEBSITE
# Implemented in order to test the above rule...

# Retirement age according to SSA website
# http://www.ssa.gov/oact/ProgData/nra.html
Tnum SSARetirementAgeInMonths(Thing p) =
    set:
    if birthYear <= 1937 -> 65 * 12
    if birthYear <= 1938 -> (65 * 12) + 2
    if birthYear <= 1939 -> (65 * 12) + 4
    if birthYear <= 1940 -> (65 * 12) + 6
    if birthYear <= 1941 -> (65 * 12) + 8
    if birthYear <= 1942 -> (65 * 12) + 10
    if birthYear <= 1954 -> 66 * 12
    if birthYear <= 1955 -> (66 * 12) + 2
    if birthYear <= 1956 -> (66 * 12) + 4
    if birthYear <= 1957 -> (66 * 12) + 6
    if birthYear <= 1958 -> (66 * 12) + 8
    if birthYear <= 1959 -> (66 * 12) + 10
    else 67 * 12

    Tnum birthYear =
        SSADateOfBirth(p).Year

        
        
# UNIT TESTS


Test: AIF_1975
- Thing p
- Peo.DoB(p) = 1975-01-04
- Fam.IsWidowed(p) = false
- Sec416.AgeIncreaseFactor(p).Out =?= 0

Test: AIF_1940
- Thing p
- Peo.DoB(p) = 1940-01-04
- Fam.IsWidowed(p) = false
- Sec416.AgeIncreaseFactor(p).Out =?= 6

Test: ERD_1975
- Thing p
- Peo.DoB(p) = 1975-01-04
- Fam.IsWidowed(p) = false
- Sec416.EarlyRetirementDate(p).Out =?= 2037-01-04

Test: ERA_1975
- Thing p
- Peo.DoB(p) = 1975-01-04
- Fam.IsWidowed(p) = false
- Sec416.EarlyRetirementAge(p).Out =?= 62

Test: ERA_1975_Widow
- Thing p
- Peo.DoB(p) = 1975-01-04
- Fam.IsWidowed(p) = true
- Sec416.EarlyRetirementAge(p).Out =?= 60

Test: ERA_1975_Unknown
- Thing p
- Peo.DoB(p) = 1975-01-04
- Sec416.EarlyRetirementAge(p).Out =?= "Unstated"

Test: RA_1975_Unwidowed
- Thing p
- Peo.DoB(p) = 1975-01-04
- Fam.IsWidowed(p) = false
- Sec416.RetirementAgeInMonths(p).Out =?= 804

Test: RA_1975_Widowed
- Thing p
- Peo.DoB(p) = 1975-01-04
- Fam.IsWidowed(p) = true
- Sec416.RetirementAgeInMonths(p).Out =?= 804

Test: AIF_1942
- Thing p
- Peo.DoB(p) = 1942-01-04
- Fam.IsWidowed(p) = false
- Sec416.AgeIncreaseFactor(p).Out =?= 10

Test: RA_1939
- Thing p
- Peo.DoB(p) = 1939-01-04
- Fam.IsWidowed(p) = false
- Sec416.SSARetirementAgeInMonths(p).Out =?= Sec416.RetirementAgeInMonths(p).Out

Test: RA_1940a
- Thing p
- Peo.DoB(p) = 1940-01-04
- Fam.IsWidowed(p) = false
- Sec416.SSARetirementAgeInMonths(p).Out =?= Sec416.RetirementAgeInMonths(p).Out

Test: RA_1940b
- Thing p
- Peo.DoB(p) = 1939-01-01
- Fam.IsWidowed(p) = false
- Sec416.SSARetirementAgeInMonths(p).Out =?= Sec416.RetirementAgeInMonths(p).Out

Test: RA_1941
- Thing p
- Peo.DoB(p) = 1941-01-04
- Fam.IsWidowed(p) = false
- Sec416.SSARetirementAgeInMonths(p).Out =?= Sec416.RetirementAgeInMonths(p).Out

Test: RA_1942
- Thing p
- Peo.DoB(p) = 1942-01-04
- Fam.IsWidowed(p) = false
- Sec416.SSARetirementAgeInMonths(p).Out =?= Sec416.RetirementAgeInMonths(p).Out

Test: RA_1944
- Thing p
- Peo.DoB(p) = 1944-01-04
- Fam.IsWidowed(p) = false
- Sec416.SSARetirementAgeInMonths(p).Out =?= Sec416.RetirementAgeInMonths(p).Out

Test: RA_1955
- Thing p
- Peo.DoB(p) = 1955-01-04
- Fam.IsWidowed(p) = false
- Sec416.SSARetirementAgeInMonths(p).Out =?= Sec416.RetirementAgeInMonths(p).Out

Test: RA_1959
- Thing p
- Peo.DoB(p) = 1959-01-04
- Fam.IsWidowed(p) = false
- Sec416.SSARetirementAgeInMonths(p).Out =?= Sec416.RetirementAgeInMonths(p).Out

Test: RA_1979
- Thing p
- Peo.DoB(p) = 1979-01-04
- Fam.IsWidowed(p) = false
- Sec416.SSARetirementAgeInMonths(p).Out =?= Sec416.RetirementAgeInMonths(p).Out

Test: DoB1
- Thing p
- Peo.DoB(p) = 1940-01-01
- Sec416.SSADateOfBirth(p).Out =?= 1939-12-31

Test: DoB2
- Thing p
- Peo.DoB(p) = 1940-01-02
- Sec416.SSADateOfBirth(p).Out =?= 1940-01-02

