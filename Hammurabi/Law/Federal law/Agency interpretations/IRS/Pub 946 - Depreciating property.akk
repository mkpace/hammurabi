# Namespace:    Hammurabi.IRS.Pub946
# Summary:      IRS Publication 946 - Depreciating Property
# Updated:      2013-01-06
# Author:       Michael Poulshock


# WHAT PROPERTY CAN BE DEPRECIATED?

# >> Can {1} depreciate {2}?
Tbool CanDepreciateProperty(Thing person, Thing property) =
    MeetsOwnershipTest(person, property) &
    UsesPropertyInBusiness(person, property) &
    HasDeterminableUsefulLife(property) &
    ExpectedLifeOfMoreThanOneYear(property)

# >>Does {1} expect to use {2} in the course of business?
Tbool UsesPropertyInBusiness(Thing person, Thing property)

# >>Does {1} have a determinable useful life?
Tbool HasDeterminableUsefulLife(Thing property)

# >>Does {1} have an expected life of more than one year?
Tbool ExpectedLifeOfMoreThanOneYear(Thing property)   
    
Tbool MeetsOwnershipTest(Thing person, Thing property) =
    Prop.Owns(person, property) |
    Stub() # lease + incidents of ownership


# WHICH DEPRECIATION SYSTEM (GDS OR ADS) APPLIES?

# Required to use the ADS depreciation system
Tbool MustUseADS(Thing person, Thing property) =
    ...
        IsListedProperty(person, property) &
        PercentageQualifiedBusinessUse(person, property) <= 50
    |
    ...
        Prop.IsTangibleProperty(property) &
        PercentageUseOutsideUS(person, property) > 50
    |
    IsTaxExemptUseProperty(property) |
    IsTaxExemptBondFinancedProperty(property) |
    ...
        PercentageUseInFarming(person, property) > 50 &
        PlacedInServiceWhenElectedNotToApplyUniformCapitalizationRules(person, property)
    |
    ...
        Prop.IsImported(property) &
        ImportedFromCountryWithEOTradeRestrictions(property)
        

# >>Does/did {1} elect to use the ADS depreciation system?  (See IRS Pub. 946, p. 36 for conditions.)
Tbool ElectsToUseADS(Thing person, Thing property)        


# WHICH GDS PROPERTY CLASS APPLIES?

##
# Applicable GDS property class
Tstr GDSPropertyClass(Thing person, Thing prop) =
    set:
    if Tbool Is3YearProperty(person, prop) -> "3-year property"
    if Tbool Is5YearProperty(person, prop) -> "5-year property"
    if Tbool Is7YearProperty(person, prop) -> "7-year property"
    if Tbool Is10YearProperty(person, prop) -> "10-year property"
    if Tbool Is15YearProperty(person, prop) -> "15-year property"
    if Tbool Is20YearProperty(person, prop) -> "20-year property"
    if Tbool Is25YearProperty(person, prop) -> "25-year property"
    if Tbool IsResidentialRentalProperty(person, prop) -> "Residential rental property"
    if Tbool IsNonresidentialRealProperty(person, prop) -> "Nonresidential real property"
    else Stub()
 ##   
    
# WHICH DEPRECIATION METHOD APPLIES?

# >>What method is {1} using to depreciate {2}? (Options: DB or SL)
Tstr DepreciationMethod(Thing person, Thing property)
    
    
# COMPUTING THE DEPRECIATION DEDUCTION W/O USING THE TABLES

# Applicable depreciate rate, given the date the property was acquired
# Returns a timeline expressing the amount of the depreciation deduction over time
Tnum DepreciationDeduction(Thing person, Thing prop) =
    Time.TemporalMap(n => Deduction(person, prop,n), serviceDate, 10, Time.IntervalType.Year)
    
    Tdate serviceDate = DatePlacedInService(person, prop)
   
# The following two functions are mutually recursive:    
    
# Basis, adjusted annually for depreciation deductions taken
Tnum AdjustedBasis(Thing person, Thing prop, Tnum year) =
    set:
    if year == 1 -> UnadjustedBasis(person, prop)
    else AdjustedBasis(person, prop, year-1) - Deduction(person, prop, year-1)
 
# Computed depreciation deduction amount
Tnum Deduction(Thing person, Thing prop, Tnum year) = 
    set:
    if DepreciationMethod(person, prop) == "DB" & year < MethodSwitchYear(recPeriod) -> (basis * DecliningBalanceRate(recPeriod) * fractionalYear).RoundToNearest(1)
    if year <= recPeriod + 1 -> (basis * StraightLineRate(yearsLeftInRecoveryPeriod) * fractionalYear).RoundToNearest(1)
    else 0
    
    Tnum basis = AdjustedBasis(person, prop, year)
    
    Tnum yearsLeftInRecoveryPeriod = 
        set:
        if year == 1 -> recPeriod
        else recPeriod - year + 2 - firstYearPercentage
    
    Tnum fractionalYear =
        set: 
        if year == 1 -> firstYearPercentage
        else 1
        
    Tnum firstYearPercentage = 
        FirstYearPercentage(ApplicableFirstYearConvention(person, prop), DatePlacedInService(person, prop))

    Tnum recPeriod = RecoveryPeriod(person, prop)
    
# Depreciation rate using the declining balance method
Tnum DecliningBalanceRate(Tnum propertyClass) =
    match propertyClass
    3   -> 0.66667
    5   -> 0.400
    7   -> 0.28571
    10  -> 0.200
    15  -> 0.100
    20  -> 0.075
    else Stub()

# Year in which you switch from declining balance to straight line method
Tnum MethodSwitchYear(Tnum propertyClass) =
    match propertyClass
    3   -> 3
    5   -> 4
    7   -> 5
    10  -> 7
    15  -> 7
    20  -> 9
    else Stub()
    
# Depreciation rate using the straight line method
Tnum StraightLineRate(Tnum yearsRemaining) =
    set:
    if yearsRemaining <= 0 -> 0
    if yearsRemaining < 1 -> 1.00
    else (1 / yearsRemaining).RoundToNearest(0.00001)

# Percentage of first-year credited towards deduction, based 
# on the applicable convention and the date the property was 
# placed in service.
Tnum FirstYearPercentage(Tstr convention, Tdate datePlacedInService) =
    set:
    if convention == "Half-year" -> 0.50
    if convention == "Mid-quarter" & datePlacedInService.Quarter == 1 -> 0.875
    if convention == "Mid-quarter" & datePlacedInService.Quarter == 2 -> 0.625
    if convention == "Mid-quarter" & datePlacedInService.Quarter == 3 -> 0.375
    if convention == "Mid-quarter" & datePlacedInService.Quarter == 4 -> 0.125
    if convention == "Mid-month" -> ((12 - datePlacedInService.Month + 0.50) / 12).RoundToNearest(0.001)
    else Stub()

 
# INPUTS

# >>When did {1} place {2} into service?
Tdate DatePlacedInService(Thing person, Thing property) 
 
Tstr ApplicableFirstYearConvention(Thing person, Thing property)

Tnum UnadjustedBasis(Thing person, Thing property)

Tnum PropertyClass(Thing person, Thing property)

Tnum RecoveryPeriod(Thing person, Thing property)

Tnum PercentageQualifiedBusinessUse(Thing person, Thing property)

Tnum PercentageUseOutsideUS(Thing person, Thing property)

Tbool IsTaxExemptUseProperty(Thing property)

Tbool IsTaxExemptBondFinancedProperty(Thing property)

Tnum PercentageUseInFarming(Thing person, Thing property)

Tbool PlacedInServiceWhenElectedNotToApplyUniformCapitalizationRules(Thing person, Thing property)

Tbool ImportedFromCountryWithEOTradeRestrictions(Thing property)

    
# LISTED PROPERTY
  
Tbool IsListedProperty(Thing person, Thing property)


# APPENDIX A: DEPRECIATION TABLES

# Returns the applicable depreciation percentage, as a time-varying percentage
Tnum DepreciationPercentage(Tbool isQIRP, Tstr system, Tstr method, Tnum recoveryPeriod, Tstr convention, Tstr assetClass, Tnum servicePeriodNumber) =
    set:
    if isQIRP -> DepreciationPercentageTablesA21ToA24(table, convention, servicePeriodNumber)
    else DepreciationPercentageTablesA1ToA18(table, recoveryPeriod)
    
    Tnum table = 
        ApplicableDepreciationTable(system, method, recoveryPeriod, convention, assetClass, servicePeriodNumber)    

# Charts 1 & 2, Appendix A, p. 75.
Tnum ApplicableDepreciationTable(Tstr system, Tstr method, Tnum recoveryPeriod, Tstr convention, Tstr assetClass, Tnum serviceQtr) =
    Stub()

# Returns a time-varying depreciation percentage, given the applicable
# table in Pub. 946 and the recovery period in years.
# See Pub. 946, Tables A-1 to A-18, pages 76-99.
Tnum DepreciationPercentageTablesA1ToA18(Tnum table, Tnum recoveryPeriod) =
    Stub()

# Returns a time-varying income inclusion percentage, given the applicable
# table in Pub. 946 and the first tax year during lease in which business
# use is 50% or less.
# See Tables A-19, A-20, p.100. 
Tnum IncomeInclusionRateTablesA19ToA20(Tnum table, Tnum yearNumber) =
    Stub()
    
# Returns a time-varying depreciation percentage, given the applicable
# table in Pub. 946, the convention used, and the period number the property
# was placed in service.
# Conventions: Half-year, Mid-quarter, Mid-month
# See Qualified Indian Reservation Property tables, A-21 to A-24, pages 101-102.
Tnum DepreciationPercentageTablesA21ToA24(Tnum table, Tstr convention, Tnum servicePeriodNumber) =
    Stub()

    
# APPENDIX B: ASSET CLASSES

# Data from Tables B-1 and B2, pages 103-113.
# Column names: Asset class, Class life, GDS recovery period, ADS recovery period
Tnum TableBValue(Tstr assetDescription, Tstr columnName) =
    Stub()
    
    
# UNIT TESTS

Test: DBRate1
- IRS.Pub946.DecliningBalanceRate(3).Out =?= 0.66667

Test: FirstYearPercentage1
- IRS.Pub946.FirstYearPercentage("Mid-month", 2011-08-04).Out =?= 0.375

Test: StraightLineRate1
- IRS.Pub946.StraightLineRate(39).Out =?= (decimal)0.02564

Test: AdjustedBasis_sanity
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 1000
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.AdjustedBasis(person, prop, 1).Out =?= 1000

Test: ComputedRate_Pub946_Page49_Ex1_Year1
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 1000
- IRS.Pub946.RecoveryPeriod(person, prop) = 5
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Half-year"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 1).Out =?= 200

Test: ComputedRate_Pub946_Page49_Ex1_Year2_AdjustedBasis
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 1000
- IRS.Pub946.RecoveryPeriod(person, prop) = 5
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Half-year"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.AdjustedBasis(person, prop, 2).Out =?= 800

Test: ComputedRate_Pub946_Page49_Ex1_Year2
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 1000
- IRS.Pub946.RecoveryPeriod(person, prop) = 5
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Half-year"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 2).Out =?= 320

Test: ComputedRate_Pub946_Page49_Ex1_Year3
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 1000
- IRS.Pub946.RecoveryPeriod(person, prop) = 5
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Half-year"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 3).Out =?= 192

Test: ComputedRate_Pub946_Page49_Ex1_Year4
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 1000
- IRS.Pub946.RecoveryPeriod(person, prop) = 5
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Half-year"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 4).Out =?= 115

Test: ComputedRate_Pub946_Page49_Ex1_Year5
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 1000
- IRS.Pub946.RecoveryPeriod(person, prop) = 5
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Half-year"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 5).Out =?= 115

Test: ComputedRate_Pub946_Page49_Ex1_Year6
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 1000
- IRS.Pub946.RecoveryPeriod(person, prop) = 5
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Half-year"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 6).Out =?= 58

Test: ComputedRate_Pub946_Page50_Ex2_Year1_FYP
- IRS.Pub946.FirstYearPercentage("Mid-month", 2011-01-02).Out =?= (decimal)0.958

Test: ComputedRate_Pub946_Page50_Ex2_Year1
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 100000
- IRS.Pub946.RecoveryPeriod(person, prop) = 39
- IRS.Pub946.DatePlacedInService(person, prop) = 2011-01-02
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Mid-month"
- IRS.Pub946.DepreciationMethod(person, prop) = "SL"
- IRS.Pub946.Deduction(person, prop, 1).Out =?= 2456 

Test: ComputedRate_Pub946_Page50_Ex2_Year2
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 100000
- IRS.Pub946.RecoveryPeriod(person, prop) = 39
- IRS.Pub946.DatePlacedInService(person, prop) = 2011-01-02
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Mid-month"
- IRS.Pub946.DepreciationMethod(person, prop) = "SL"
- IRS.Pub946.Deduction(person, prop, 2).Out =?= 2564

Test: ComputedRate_Pub946_Page50_Ex2_Year3
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 100000
- IRS.Pub946.RecoveryPeriod(person, prop) = 39
- IRS.Pub946.DatePlacedInService(person, prop) = 2011-01-02
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Mid-month"
- IRS.Pub946.DepreciationMethod(person, prop) = "SL"
- IRS.Pub946.Deduction(person, prop, 3).Out =?= 2564

Test: ComputedRate_Pub946_Page50_Ex3_Safe_Year1
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 4000
- IRS.Pub946.RecoveryPeriod(person, prop) = 7
- IRS.Pub946.DatePlacedInService(person, prop) = 2011-01-02
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Mid-quarter"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 1).Out =?= 1000

Test: ComputedRate_Pub946_Page50_Ex3_Safe_Year2
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 4000
- IRS.Pub946.RecoveryPeriod(person, prop) = 7
- IRS.Pub946.DatePlacedInService(person, prop) = 2011-01-02
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Mid-quarter"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 2).Out =?= 857

Test: ComputedRate_Pub946_Page50_Ex3_Furniture_Year1
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 1000
- IRS.Pub946.RecoveryPeriod(person, prop) = 7
- IRS.Pub946.DatePlacedInService(person, prop) = 2011-09-02
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Mid-quarter"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 1).Out =?= 107

Test: ComputedRate_Pub946_Page50_Ex3_Furniture_Year2
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 1000
- IRS.Pub946.RecoveryPeriod(person, prop) = 7
- IRS.Pub946.DatePlacedInService(person, prop) = 2011-09-02
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Mid-quarter"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 2).Out =?= 255

Test: ComputedRate_Pub946_Page50_Ex3_Computer_Year1
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 5000
- IRS.Pub946.RecoveryPeriod(person, prop) = 5
- IRS.Pub946.DatePlacedInService(person, prop) = 2011-10-02
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Mid-quarter"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 1).Out =?= 250

Test: ComputedRate_Pub946_Page50_Ex3_Computer_Year2
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 5000
- IRS.Pub946.RecoveryPeriod(person, prop) = 5
- IRS.Pub946.DatePlacedInService(person, prop) = 2011-10-02
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Mid-quarter"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 2).Out =?= 1900

Test: ComputedRate_Pub946_Page51_Ex4_Year1
- Things prop, person
- IRS.Pub946.UnadjustedBasis(person, prop) = 7500
- IRS.Pub946.RecoveryPeriod(person, prop) = 7
- IRS.Pub946.DatePlacedInService(person, prop) = 2011-07-02
- IRS.Pub946.ApplicableFirstYearConvention(person, prop) = "Half-year"
- IRS.Pub946.DepreciationMethod(person, prop) = "DB"
- IRS.Pub946.Deduction(person, prop, 1).Out =?= 1071  # $1 diff b/c example uses tables
    
