# Namespace:    Hammurabi.IRS.Pub501
# Summary:      IRS Publication 501 - Who must file a tax return
# Updated:      2012-10-05
# Author:       Michael Poulshock


# RULES FOR TAX YEAR 2011

Tbool MustFileTaxReturn(Thing p) =
    set:
    if IRS.Pub519.IsNonresidentAlien(p) -> Stub()
    if HasIncomeFromUSPossession(p) -> Stub()
    if Peo.IsDeceased(p) -> DeceasedPersonMustFile(p)
    else CoreMustFileTest(p)

Tbool CoreMustFileTest(Thing p) =
    ...
        USC.Tit26.Sec152.CanBeClaimedAsDependentBySomeone(p) &
        MustFilePerTable2(p)
    |
    ...
        ! USC.Tit26.Sec152.CanBeClaimedAsDependentBySomeone(p) &
        MustFilePerTable1(p)
    |
    MustFilePerTable3(p)

    
# TABLE 1 - MOST TAXPAYERS

# Table 1 (page 3)
Tbool MustFilePerTable1(Thing p) =
    Table1Applies(status, TaxAge(p), GrossIncomeForFS(p), SpousesUnder65(p), LivedWithSpouseForTaxPurposes(p))
        
    Tstr status = USC.Tit26.Sec2.FilingStatus(p) 

Tbool Table1Applies(Tstr status, Tnum age, Tnum grossIncome, Tnum spousesUnder65, Tbool livedWSpouse) =
    match status,               age,        grossIncome,    spousesUnder65, livedWSpouse
    Single,                     < 65,       >= 9500,,                                       -> true
    Single,                     >= 65,      >= 10950,,                                      -> true
    Head of household,          < 65,       >= 12200,,                                      -> true
    Head of household,          >= 65,      >= 13650,,                                      -> true
    Married filing jointly,,,,                                              false           -> true 
    Married filing jointly,,    >= 19000,                   2,                              -> true
    Married filing jointly,,    >= 20150,                   1,                              -> true
    Married filing jointly,,    >= 21300,                   0,                              -> true
    Married filing separately,, >= 3700,,                                                   -> true
    Qualifying widow(er),       < 65,       >= 15300,,                                      -> true
    Qualifying widow(er),       >= 65,      >= 16450,,                                      -> true
    else false

# Number of people (person/spouse) under age 65    
Tnum SpousesUnder65(Thing p) =
    Fam.SelfAndSpouseIfMarried(p).Filter(TaxAge(_) < 65).Count


# TABLE 2 - DEPENDENTS
        
# Table 2 (page 4) - filing requirements for dependents
Tbool MustFilePerTable2(Thing p) =
    MustFilePerTable2Single(p) |
    MustFilePerTable2Married(p)
    
# Table 2 - single dependents
Tbool MustFilePerTable2Single(Thing p) =
    Fam.MaritalStatus(p) == "Single" &
    ...
        ...
            ! Over65OrBlind(p) &
            ...
                UnearnedIncome(p) > $950 |
                EarnedIncomeForFS(p) > $5,800 |
                GrossIncomeForFS(p) > Max(950, Min(EarnedIncomeForFS(p), $5,500) + $300)
        |
        ...
            Over65OrBlind(p) & ! Over65AndBlind(p) &     # either 65+ or blind, but not both
            ...
                UnearnedIncome(p) > $2,400 |
                EarnedIncomeForFS(p) > $7,250 |
                GrossIncomeForFS(p) > Max(2400, Min(EarnedIncomeForFS(p), $5,500) + $1,750)
        |
        ...
            Over65AndBlind(p) &
            ...
                UnearnedIncome(p) > $3,850 |
                EarnedIncomeForFS(p) > $8,700 |
                GrossIncomeForFS(p) > Max(3850, Min(EarnedIncomeForFS(p), $5,500) + $3,200)          

# Table 2 - married dependents
Tbool MustFilePerTable2Married(Thing p) =
    Fam.MaritalStatus(p) == "Married" &
    ...
        ...
            ! Over65OrBlind(p) &
            ...
                MeetsSpouseItemizingTest(p) |
                UnearnedIncome(p) > 950 |
                EarnedIncomeForFS(p) > 5800 |
                GrossIncomeForFS(p) > Max(950, Min(EarnedIncomeForFS(p), 5500) + 300)
        |
        ...
            Over65OrBlind(p) & ! Over65AndBlind(p) &     # either 65+ or blind, but not both
            ...
                MeetsSpouseItemizingTest(p) |
                UnearnedIncome(p) > 2100 |
                EarnedIncomeForFS(p) > 6950 |
                GrossIncomeForFS(p) > Max(2100, Min(EarnedIncomeForFS(p), 5500) + 1450)
        |
        ...
            Over65AndBlind(p) &
            ...
                MeetsSpouseItemizingTest(p) |
                UnearnedIncome(p) > 3250 |
                EarnedIncomeForFS(p) > 8100 |
                GrossIncomeForFS(p) > Max(3250, Min(EarnedIncomeForFS(p), 5500) + 2600)    
                
Tbool Over65OrBlind(Thing p) =
    TaxAge(p) >= 65 |
    Peo.IsBlind(p)

Tbool Over65AndBlind(Thing p) =
    TaxAge(p) >= 65 &
    Peo.IsBlind(p)

Tbool MeetsSpouseItemizingTest(Thing p) =
    GrossIncomeForFS(p) > 5 & 
    USC.Tit26.Sec2.FilingStatus(p) == "Married filing separately" &
    Fam.SpousesOf(p).Exists(ItemizesDeductions(_))


# TABLE 3

# Table 3 (page 5) - other situations when you must file
Tbool MustFilePerTable3(Thing p) =
    OwesAlternativeMinimumTax(p) |
    OwesAdditionalTaxOnQualifiedPlan(p) |
    OwesSocSecOrMedicareTaxes(p) |
    OwesWriteInTaxes(p) |
    OwesHouseholdEmploymentTaxes(p) |
    OwesRecaptureTaxes(p) |
    HadHSADistribution(p) |
    NetEarningsFromSelfEmployment(p) >= 400 |
    HadThresholdChurchWages(p)

# >>Does/did {1} owe the alternative minimum tax (AMT)?
Tbool OwesAlternativeMinimumTax(Thing p)

# >>Does/did {1} owe an additional tax on a qualified plan?
Tbool OwesAdditionalTaxOnQualifiedPlan(Thing p)

# >>Does/did {1} owe Social Security or Medicare taxes on unreported tips or wages that were not withheld?
Tbool OwesSocSecOrMedicareTaxes(Thing p)

# >>Does/did {1} owe write-in taxes?
Tbool OwesWriteInTaxes(Thing p)

# >>Does/did {1} owe household employment taxes?
Tbool OwesHouseholdEmploymentTaxes(Thing p)

# >>Does/did {1} owe recapture taxes?
Tbool OwesRecaptureTaxes(Thing p)

# >>Does/did {1} (or their spouse if filing jointly) receive Archer MSA, Medicare Advantage MSA, or health savings account distributions?
Tbool HadHSADistribution(Thing p)

# >>Does/did {1} have over 108.28 in wages from a qualifying church-related organization?
Tbool HadThresholdChurchWages(Thing p)
	
# >>What are/were {1}'s net earnings from self-employment?
Tnum NetEarningsFromSelfEmployment(Thing p)


# TODO: Move these elsewhere...?

# Deceased person's situation is analyzed as of their date of death
Tbool DeceasedPersonMustFile(Thing p) =
    CoreMustFileTest(p).AsOf(Peo.DateOfDeath(p))
    
# Age on the last day of the tax year
Tnum TaxAge(Thing p) =
    age.PeriodEndVal(year)
    
    Tnum year = USC.Tit26.Sec441.TaxYear(p)

    Tnum age = Time.IntervalsSince(dob, dob.AddYears(110), Time.IntervalType.Year)
        
    Tdate dob = Peo.DoB(p).AddDays(-1)    # Jan. 1 is a freebie
    
# Marital status for tax purposes (determined on last day of tax year)
Tstr MaritalStatusForTaxPurposes(Thing p) =
    Fam.MaritalStatus(p).PeriodEndVal(year)
    
    Tnum year = USC.Tit26.Sec441.TaxYear(p)
    
# Lived with spouse for tax purposes
Tbool LivedWithSpouseForTaxPurposes(Thing p) =
    Fam.SpousesOf(p).Exists(LivedTogetherForTaxPurposes(p,_))
    
Tbool LivedTogetherForTaxPurposes(Thing spouse, Thing deceasedSpouse) =
    ...
        Peo.IsDeceased(deceasedSpouse) &
        Econ.LivesWith(spouse,deceasedSpouse).AsOf(Peo.DateOfDeath(deceasedSpouse)) &
        Time.IsBefore(Peo.DateOfDeath(deceasedSpouse).AddYears(1)).PeriodEndVal(year)    # ugly, but prevents eternal truth
    |
    Econ.LivesWith(spouse,deceasedSpouse).PeriodEndVal(year)
    
    # Tdate death = Peo.DateOfDeath(deceasedSpouse)
    
    Tnum year = USC.Tit26.Sec441.TaxYear(spouse)

    
# INPUTS

# >>Does/did {1} itemize deductions?
Tbool ItemizesDeductions(Thing p)

# >>Does/did {1} have income from a U.S. possession (Guam, N. Mariana Islands, American Samoa, or the U.S. Virgin Islands)?
Tbool HasIncomeFromUSPossession(Thing p)


# UNIT TESTS

Test: 878841534
- Thing ling
- USC.Tit26.Sec2.FedTaxFilingStatus(ling) = "Married filing jointly"
- Peo.DoB(ling) = 1910-01-01
- IRS.Pub501.GrossIncomeForFS(ling) = 21000
- Things bing
- Fam.SpousesOf(ling) = [[bing]]
- Peo.DoB(bing) = 1910-01-01
- Peo.IsDeceased(bing) = false
- Econ.LivesWith(bing,ling) = true
- IRS.Pub501.MustFilePerTable1(ling).Out =?= "{Dawn: true; 1/1/1974: false; 1/1/2019: true}"

Test: LivedTogether1
- Things mary, tony
- Peo.IsDeceased(tony) = true
- Peo.DateOfDeath(tony) = 2012-05-01
- Econ.LivesWith(tony,mary) = {Dawn: true; 2012-05-02: false}
- IRS.Pub501.LivedTogetherForTaxPurposes(mary, tony).Out =?= "{Dawn: true; 1/1/2013: false}"

Test: LivedTogether2
- Things mary, tony
- Peo.IsDeceased(tony) = false
- Econ.LivesWith(tony,mary) = {Dawn: true; 2012-05-02: false}
- IRS.Pub501.LivedTogetherForTaxPurposes(mary, tony).Out =?= "{Dawn: true; 1/1/2012: false}"

Test: TaxAge1
- Thing mary
- Peo.DoB(mary) = 1947-01-02
- Peo.IsBlind(mary) = false
- IRS.Pub501.Over65OrBlind(mary).Out =?= "{Dawn: false; 1/1/2012: true; 1/1/2057: false}"

Test: TaxAge2
- Thing mary
- Peo.DoB(mary) = 1947-01-01
- Peo.IsBlind(mary) = false
- IRS.Pub501.Over65OrBlind(mary).Out =?= "{Dawn: false; 1/1/2011: true; 1/1/2056: false}"

Test: TaxMaritalStatus
- Thing jim
- Fam.MaritalStatus(jim) = {Dawn: "Single"; 2012-05-02: "Married"}
- IRS.Pub501.MaritalStatusForTaxPurposes(jim).Out =?= "{Dawn: Single; 1/1/2012: Married}"

Test: 738117373
- Thing liam
- USC.Tit26.Sec152.CanBeClaimedAsDependentBySomeone(liam) = true
- Fam.MaritalStatus(liam) = "Single"
- Peo.DoB(liam) = 1975-01-01
- Peo.IsBlind(liam) = false
- IRS.Pub501.UnearnedIncome(liam) = 2450
- IRS.Pub501.MustFilePerTable2(liam).Out =?= true

Test: 417756183
- Things ling, john
- USC.Tit26.Sec2.FedTaxFilingStatus(ling) = "Married filing jointly"
- Peo.DoB(ling) = 1975-01-01
- IRS.Pub501.GrossIncomeForFS(ling) = 19001
- Fam.SpousesOf(ling) = [[john]]
- Peo.DoB(john) = 1981-01-01
- IRS.Pub501.MustFilePerTable1(ling).Out =?= "{Dawn: true; 1/1/2040: false; 1/1/2091: true}"

Test: 189988938
- Thing jam
- USC.Tit26.Sec2.FedTaxFilingStatus(jam) = "Qualifying widow(er)"
- Peo.DoB(jam) = 1980-02-02
- IRS.Pub501.GrossIncomeForFS(jam) = 16451
- IRS.Pub501.MustFilePerTable1(jam).Out =?= true

Test: 844051182
- Thing lonnie
- Peo.DoB(lonnie) = 1980-01-01
- USC.Tit26.Sec2.FedTaxFilingStatus(lonnie) = "Married filing separately"
- IRS.Pub501.GrossIncomeForFS(lonnie) = 9
- IRS.Pub501.MustFilePerTable1(lonnie).Out =?= false

