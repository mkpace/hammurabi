# Citation:     8 U.S.C. 1407 (2012)
# Namespace:    Hammurabi.USC.Tit8.Sec1407
# Summary:      Persons living in and born in Guam
# Updated:      2012-09-15
# Author:       Michael Poulshock


# Citizenship - Guam
Tbool IsUSCitizenPerGuam(Thing p) = 
    ModernTest(p) |
    Catchall(p)
    
# Current test for citizenship
Tbool ModernTest(Thing p) =
    Peo.DoB(p) >= 1899-04-11 &
    Peo.BirthCountry(p) == "Guam" &
    Peo.IsSubjectToUSJurisdiction(p) &
    if Peo.DoB(p) < 1950-08-01 then !TookStepsToPreserveForeignNationality(p) &        # Proviso
    Time.IsAtOrAfter(Peo.DoB(p))                # Presumed
    
# Stub if DoB is before 1899-04-11
Tbool Catchall(Thing p) =
    Peo.DoB(p) < 1899-04-11 &
    Peo.BirthCountry(p) == "Guam" &
    Stub()
    
# >>Did {1} take affirmative steps to preserve or acquire foreign nationality?
Tbool TookStepsToPreserveForeignNationality(Thing p)


# UNIT TESTS

Test: 701366647
- Thing murray
- Peo.DoB(murray) = 1988-03-04
- Peo.BirthCountry(murray) = "Guam"
- Peo.IsSubjectToUSJurisdiction(murray) = true
- USC.Tit8.Sec1407.IsUSCitizenPerGuam(murray).Out =?= "{Dawn: false; 3/4/1988: true}"

Test: 904438779
- Thing murray
- Peo.DoB(murray) = 1904-01-02
- Peo.BirthCountry(murray) = "Guam"
- Peo.IsSubjectToUSJurisdiction(murray) = true
- USC.Tit8.Sec1407.TookStepsToPreserveForeignNationality(murray) = false
- USC.Tit8.Sec1407.IsUSCitizenPerGuam(murray).Out =?= "{Dawn: false; 1/2/1904: true}"

Test: 65134483
- Thing murray
- Peo.DoB(murray) = 1890-01-01
- Peo.BirthCountry(murray) = "Guam"
- USC.Tit8.Sec1407.IsUSCitizenPerGuam(murray).Out =?= "Stub"

